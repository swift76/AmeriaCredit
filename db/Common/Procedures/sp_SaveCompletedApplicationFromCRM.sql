if exists (select * from sys.objects where name='sp_SaveCompletedApplicationFromCRM' and type='P')
	drop procedure Common.sp_SaveCompletedApplicationFromCRM
GO

create procedure Common.sp_SaveCompletedApplicationFromCRM(
	@APPLICATION_ID 			uniqueidentifier,
	@INTEREST					money,
	@FINAL_AMOUNT				money,
	@PERIOD_TYPE_CODE			char(3),
	@REPAY_DAY					tinyint,
	@FIRST_NAME_EN				varchar(20),
	@LAST_NAME_EN				varchar(20),
	@MOBILE_PHONE_1				varchar(20),
	@MOBILE_PHONE_2				varchar(20),
	@FIXED_PHONE				varchar(20),
	@EMAIL						varchar(70),
	@BIRTH_PLACE_CODE			char(2),
	@CITIZENSHIP_CODE			char(2),
	@REGISTRATION_COUNTRY_CODE	char(2),
	@REGISTRATION_STATE_CODE	char(3),
	@REGISTRATION_CITY_CODE		char(9),
	@REGISTRATION_STREET		nvarchar(150),
	@REGISTRATION_BUILDNUM		nvarchar(30),
	@REGISTRATION_APARTMENT		nvarchar(30),
	@CURRENT_COUNTRY_CODE		char(2),
	@CURRENT_STATE_CODE			char(3),
	@CURRENT_CITY_CODE			char(9),
	@CURRENT_STREET				nvarchar(150),
	@CURRENT_BUILDNUM			nvarchar(30),
	@CURRENT_APARTMENT			nvarchar(30),
	@FAMILY_STATUS_CODE			char(1),
	@LOAN_TEMPLATE_CODE			char(4),
	@OVERDRAFT_TEMPLATE_CODE	char(4),
	@EXISTING_CARD_CODE			char(16),
	@IS_NEW_CARD				bit,
	@CREDIT_CARD_TYPE_CODE		char(3),
	@IS_CARD_DELIVERY			bit,
	@CARD_DELIVERY_ADDRESS		nvarchar(150),
	@BANK_BRANCH_CODE			char(3),
	@IS_ARBITRAGE_CHECKED		bit
)
AS
	BEGIN TRANSACTION

	BEGIN TRY
		declare @STATUS tinyint, @IMPORT_ID int

		select @STATUS = STATUS, @IMPORT_ID = IMPORT_ID
		from Common.APPLICATION with (updlock) where ID = @APPLICATION_ID

		if not @STATUS in (5, 8)
			RAISERROR (N'Հայտը տվյալների լրացման կարգավիճակում չէ', 17, 1)

		delete from Common.COMPLETED_APPLICATION where APPLICATION_ID = @APPLICATION_ID
		insert into Common.COMPLETED_APPLICATION
			(APPLICATION_ID,INTEREST,FINAL_AMOUNT,PERIOD_TYPE_CODE,REPAY_DAY,FIRST_NAME_EN,LAST_NAME_EN,MOBILE_PHONE_1,MOBILE_PHONE_2,FIXED_PHONE,EMAIL,BIRTH_PLACE_CODE,CITIZENSHIP_CODE
				,REGISTRATION_COUNTRY_CODE,REGISTRATION_STATE_CODE,REGISTRATION_CITY_CODE,REGISTRATION_STREET,REGISTRATION_BUILDNUM,REGISTRATION_APARTMENT
				,CURRENT_COUNTRY_CODE,CURRENT_STATE_CODE,CURRENT_CITY_CODE,CURRENT_STREET,CURRENT_BUILDNUM,CURRENT_APARTMENT
				,FAMILY_STATUS_CODE,LOAN_TEMPLATE_CODE,OVERDRAFT_TEMPLATE_CODE)
		values
			(@APPLICATION_ID,@INTEREST,@FINAL_AMOUNT,@PERIOD_TYPE_CODE,@REPAY_DAY,@FIRST_NAME_EN,@LAST_NAME_EN,@MOBILE_PHONE_1,@MOBILE_PHONE_2,@FIXED_PHONE,@EMAIL,@BIRTH_PLACE_CODE,@CITIZENSHIP_CODE
				,@REGISTRATION_COUNTRY_CODE,@REGISTRATION_STATE_CODE,@REGISTRATION_CITY_CODE,@REGISTRATION_STREET,@REGISTRATION_BUILDNUM,@REGISTRATION_APARTMENT
				,@CURRENT_COUNTRY_CODE,@CURRENT_STATE_CODE,@CURRENT_CITY_CODE,@CURRENT_STREET,@CURRENT_BUILDNUM,@CURRENT_APARTMENT
				,@FAMILY_STATUS_CODE,@LOAN_TEMPLATE_CODE,@OVERDRAFT_TEMPLATE_CODE)

		delete from GL.AGREED_APPLICATION
			where APPLICATION_ID = @APPLICATION_ID
		insert into GL.AGREED_APPLICATION (
			APPLICATION_ID,
			EXISTING_CARD_CODE,
			IS_NEW_CARD,
			CREDIT_CARD_TYPE_CODE,
			IS_CARD_DELIVERY,
			CARD_DELIVERY_ADDRESS,
			BANK_BRANCH_CODE,
			IS_ARBITRAGE_CHECKED)
		values (
			@APPLICATION_ID,
			@EXISTING_CARD_CODE,
			@IS_NEW_CARD,
			@CREDIT_CARD_TYPE_CODE,
			@IS_CARD_DELIVERY,
			@CARD_DELIVERY_ADDRESS,
			@BANK_BRANCH_CODE,
			@IS_ARBITRAGE_CHECKED)

		if @IMPORT_ID>0
			update Common.APPLICATION
				set STATUS=16,TO_BE_SYNCHRONIZED=1
			where IMPORT_ID=@IMPORT_ID and ID<>@APPLICATION_ID

		execute Common.sp_ChangeApplicationStatus @APPLICATION_ID,15

		COMMIT TRANSACTION
	END TRY
	BEGIN CATCH
		ROLLBACK TRANSACTION
		declare @ErrorMessage nvarchar(4000) = ERROR_MESSAGE()
		RAISERROR (@ErrorMessage, 17, 1)
	END CATCH
GO
