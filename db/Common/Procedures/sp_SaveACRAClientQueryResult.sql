if exists (select * from sys.objects where name='sp_SaveACRAClientQueryResult' and type='P')
	drop procedure Common.sp_SaveACRAClientQueryResult
GO

create procedure Common.sp_SaveACRAClientQueryResult(@SOCIAL_CARD_NUMBER	char(10),
													 @USER_ID				char(4),
													 @IS_BLOCKED			bit,
													 @FIRST_NAME			nvarchar(20),
													 @LAST_NAME				nvarchar(20),
													 @BIRTH_DATE			date,
													 @PASSPORT_NUMBER		char(9),
													 @ID_CARD_NUMBER		char(9),
													 @FICO_SCORE			char(3),
													 @RESPONSE_XML			nvarchar(max),
													 @DETAILS				Common.ACRAQueryResultDetails		readonly,
													 @QUERIES				Common.ACRAQueryResultQueries		readonly,
													 @INTERRELATED			Common.ACRAQueryResultInterrelated	readonly)
AS
	BEGIN TRANSACTION
	BEGIN TRY
		declare @CURRENT_DATE date=getdate(),@ID uniqueidentifier=newid()
		delete from Common.ACRA_CLIENT_QUERY_RESULT
		where SOCIAL_CARD_NUMBER=@SOCIAL_CARD_NUMBER
			and convert(date,QUERY_DATE)=@CURRENT_DATE

		insert into Common.ACRA_CLIENT_QUERY_RESULT
			(ID,SOCIAL_CARD_NUMBER,IS_BLOCKED,USER_ID,FIRST_NAME,LAST_NAME,BIRTH_DATE,PASSPORT_NUMBER,ID_CARD_NUMBER,FICO_SCORE,RESPONSE_XML)
		values
			(@ID,@SOCIAL_CARD_NUMBER,@IS_BLOCKED,@USER_ID,@FIRST_NAME,@LAST_NAME,@BIRTH_DATE,@PASSPORT_NUMBER,@ID_CARD_NUMBER,@FICO_SCORE,@RESPONSE_XML)

		delete from Common.ACRA_CLIENT_QUERY_RESULT_DETAILS where RESULT_ID=@ID
		insert into Common.ACRA_CLIENT_QUERY_RESULT_DETAILS
			(RESULT_ID,STATUS,FROM_DATE,TO_DATE,TYPE,CUR,CONTRACT_AMOUNT,DEBT,PAST_DUE_DATE,RISK,CLASSIFICATION_DATE,
				INTEREST_RATE,PLEDGE,PLEDGE_AMOUNT,OUTSTANDING_AMOUNT,OUTSTANDING_PERCENT,BANK_NAME,IS_GUARANTEE,
				DUE_DAYS_1,DUE_DAYS_2,DUE_DAYS_3,DUE_DAYS_4,LOAN_ID)
		select @ID,STATUS,FROM_DATE,TO_DATE,TYPE,CUR,CONTRACT_AMOUNT,DEBT,PAST_DUE_DATE,RISK,CLASSIFICATION_DATE,
				INTEREST_RATE,PLEDGE,PLEDGE_AMOUNT,OUTSTANDING_AMOUNT,OUTSTANDING_PERCENT,BANK_NAME,IS_GUARANTEE,
				DUE_DAYS_1,DUE_DAYS_2,DUE_DAYS_3,DUE_DAYS_4,LOAN_ID
			from @DETAILS

		delete from Common.ACRA_CLIENT_QUERY_RESULT_QUERIES where RESULT_ID=@ID
		insert into Common.ACRA_CLIENT_QUERY_RESULT_QUERIES
			(RESULT_ID,DATE,BANK_NAME)
		select @ID,DATE,BANK_NAME
			from @QUERIES

		delete from Common.ACRA_CLIENT_QUERY_RESULT_INTERRELATED where RESULT_ID=@ID
		insert into Common.ACRA_CLIENT_QUERY_RESULT_INTERRELATED
			(RESULT_ID,FROM_DATE,TO_DATE,CUR,RISK,CONTRACT_AMOUNT,DUE_AMOUNT,OVERDUE_AMOUNT,OUTSTANDING_PERCENT)
		select @ID,FROM_DATE,TO_DATE,CUR,RISK,CONTRACT_AMOUNT,DUE_AMOUNT,OVERDUE_AMOUNT,OUTSTANDING_PERCENT
			from @INTERRELATED
	END TRY
	BEGIN CATCH
		ROLLBACK TRANSACTION
		declare @ErrorMessage varchar(4000)
		set @ErrorMessage = ERROR_MESSAGE()
		RAISERROR (@ErrorMessage, 17, 1)
		RETURN
	END CATCH
	COMMIT TRANSACTION
GO
