alter table Common.APPLICATION
add
CREATE_CLIENT_ONLY			bit				NULL
GO



create or alter procedure Common.sp_SaveApplicationForScoring(
	@SOURCE_ID						tinyint,
	@LOAN_TYPE_ID					char(2),
	@FIRST_NAME						nvarchar(20)	= null,
	@LAST_NAME						nvarchar(20)	= null,
	@PATRONYMIC_NAME				nvarchar(20)	= null,
	@BIRTH_DATE						date			= null,
	@SOCIAL_CARD_NUMBER				char(10),
	@DOCUMENT_TYPE_CODE				char(1)			= null,
	@DOCUMENT_NUMBER				char(9)			= null,
	@DOCUMENT_GIVEN_BY				char(3)			= null,
	@DOCUMENT_GIVEN_DATE			date			= null,
	@DOCUMENT_EXPIRY_DATE			date			= null,
	@INITIAL_AMOUNT					money			= null,
	@CURRENCY_CODE					char(3)			= null,
	@ORGANIZATION_ACTIVITY_CODE		char(2)			= null,
	@MOBILE_PHONE_1					varchar(20) 	= null,
	@CUSTOMER_USER_ID				int				= null,
	@SHOP_USER_ID					int				= null,
	@PARTNER_COMPANY_CODE			varchar(8)		= null,
	@PRODUCT_CATEGORY_CODE			char(2)			= null,
	@LOAN_TEMPLATE_CODE				char(4)			= null,
	@IS_REFINANCING					bit = 0,
	@ID								uniqueidentifier	output,
	@COBORROWER_SOCIAL_CARD_NUMBER	char(10) = NULL,
	@COBORROWER_FIRST_NAME			nvarchar(20) = NULL,
	@COBORROWER_LAST_NAME			nvarchar(20) = NULL,
	@COBORROWER_BIRTH_DATE			date = NULL,
	@COBORROWER_FIRST_NAME_EN		varchar(20) = NULL,
	@COBORROWER_LAST_NAME_EN		varchar(20) = NULL,
	@COBORROWER_MOBILE_PHONE		varchar(20) = NULL,
	@COBORROWER_EMAIL				varchar(70) = NULL,
	@COBORROWER_BIRTH_PLACE_CODE	char(2) = NULL,
	@COBORROWER_CITIZENSHIP_CODE	char(2) = NULL,
	@SKIP_OTI						bit = NULL,
	@UNIVERSITY_CODE				char(2) = NULL,
	@UNIVERSITY_FACULTY				nvarchar(50) = NULL,
	@UNIVERSITY_YEAR				nvarchar(50) = NULL,
	@CREATE_CLIENT_ONLY				bit	= 0,

	@NORQ_FIRST_NAME								nvarchar(20),
	@NORQ_LAST_NAME									nvarchar(20),
	@NORQ_PATRONYMIC_NAME							nvarchar(20),
	@NORQ_BIRTH_DATE								date,
	@NORQ_GENDER									bit,
	@NORQ_DISTRICT									nvarchar(20),
	@NORQ_COMMUNITY									nvarchar(40),
	@NORQ_STREET									nvarchar(100),
	@NORQ_BUILDING									nvarchar(40),
	@NORQ_APARTMENT									nvarchar(40),
	@NORQ_FEE										money,
	@NORQ_PASSPORT_NUMBER							char(9) = NULL,
	@NORQ_PASSPORT_DATE								date = NULL,
	@NORQ_PASSPORT_EXPIRY_DATE						date = NULL,
	@NORQ_PASSPORT_BY								char(3) = NULL,
	@NORQ_BIOMETRIC_PASSPORT_NUMBER					char(9) = NULL,
	@NORQ_BIOMETRIC_PASSPORT_ISSUE_DATE				date = NULL,
	@NORQ_BIOMETRIC_PASSPORT_EXPIRY_DATE			date = NULL,
	@NORQ_BIOMETRIC_PASSPORT_ISSUED_BY				char(3) = NULL,
	@NORQ_ID_CARD_NUMBER							char(9) = NULL,
	@NORQ_ID_CARD_ISSUE_DATE						date = NULL,
	@NORQ_ID_CARD_EXPIRY_DATE						date = NULL,
	@NORQ_ID_CARD_ISSUED_BY							char(3) = NULL,
	@NORQ_SOCIAL_CARD_NUMBER						char(10),
	@NORQ_SOCIAL_PAYMENT							money,
	@NORQ_RESPONSE_XML								nvarchar(max),
	@NORQ_WORK_DATA									Common.NORQQueryResultWork	readonly,

	@NORQ_COBORROWER_FIRST_NAME						nvarchar(20) = NULL,
	@NORQ_COBORROWER_LAST_NAME						nvarchar(20) = NULL,
	@NORQ_COBORROWER_PATRONYMIC_NAME				nvarchar(20) = NULL,
	@NORQ_COBORROWER_BIRTH_DATE						date = NULL,
	@NORQ_COBORROWER_GENDER							bit = NULL,
	@NORQ_COBORROWER_DISTRICT						nvarchar(20) = NULL,
	@NORQ_COBORROWER_COMMUNITY						nvarchar(40) = NULL,
	@NORQ_COBORROWER_STREET							nvarchar(100) = NULL,
	@NORQ_COBORROWER_BUILDING						nvarchar(40) = NULL,
	@NORQ_COBORROWER_APARTMENT						nvarchar(40) = NULL,
	@NORQ_COBORROWER_FEE							money = NULL,
	@NORQ_COBORROWER_PASSPORT_NUMBER				char(9) = NULL,
	@NORQ_COBORROWER_PASSPORT_DATE					date = NULL,
	@NORQ_COBORROWER_PASSPORT_EXPIRY_DATE			date = NULL,
	@NORQ_COBORROWER_PASSPORT_BY					char(3) = NULL,
	@NORQ_COBORROWER_BIOMETRIC_PASSPORT_NUMBER		char(9) = NULL,
	@NORQ_COBORROWER_BIOMETRIC_PASSPORT_ISSUE_DATE	date = NULL,
	@NORQ_COBORROWER_BIOMETRIC_PASSPORT_EXPIRY_DATE	date = NULL,
	@NORQ_COBORROWER_BIOMETRIC_PASSPORT_ISSUED_BY	char(3) = NULL,
	@NORQ_COBORROWER_ID_CARD_NUMBER					char(9) = NULL,
	@NORQ_COBORROWER_ID_CARD_ISSUE_DATE				date = NULL,
	@NORQ_COBORROWER_ID_CARD_EXPIRY_DATE			date = NULL,
	@NORQ_COBORROWER_ID_CARD_ISSUED_BY				char(3) = NULL,
	@NORQ_COBORROWER_SOCIAL_CARD_NUMBER				char(10) = NULL,
	@NORQ_COBORROWER_SOCIAL_PAYMENT					money = NULL,
	@NORQ_COBORROWER_RESPONSE_XML					nvarchar(max) = NULL,
	@NORQ_COBORROWER_WORK_DATA						Common.NORQQueryResultWork	readonly,

	@ACRA_FICO_SCORE				char(3),
	@ACRA_RESPONSE_XML				nvarchar(max),
	@ACRA_DETAILS					Common.ACRAQueryResultDetails		readonly,
	@ACRA_QUERIES					Common.ACRAQueryResultQueries		readonly,
	@ACRA_INTERRELATED				Common.ACRAQueryResultInterrelated	readonly,

	@ACRA_COBORROWER_FICO_SCORE		char(3) = null,
	@ACRA_COBORROWER_RESPONSE_XML	nvarchar(max) = null,
	@ACRA_COBORROWER_DETAILS		Common.ACRAQueryResultDetails		readonly,
	@ACRA_COBORROWER_QUERIES		Common.ACRAQueryResultQueries		readonly,
	@ACRA_COBORROWER_INTERRELATED	Common.ACRAQueryResultInterrelated	readonly
)
AS
	set @ID = newid()
	declare @ErrorMessage nvarchar(4000)
	declare @CurrentDate date = getdate()

	if @DOCUMENT_NUMBER=@NORQ_PASSPORT_NUMBER
	begin
		set @DOCUMENT_GIVEN_BY = @NORQ_PASSPORT_BY
		set @DOCUMENT_GIVEN_DATE = @NORQ_PASSPORT_DATE
		set @DOCUMENT_EXPIRY_DATE = @NORQ_PASSPORT_EXPIRY_DATE
	end
	else if @DOCUMENT_NUMBER=@NORQ_BIOMETRIC_PASSPORT_NUMBER
	begin
		set @DOCUMENT_GIVEN_BY = @NORQ_BIOMETRIC_PASSPORT_ISSUED_BY
		set @DOCUMENT_GIVEN_DATE = @NORQ_BIOMETRIC_PASSPORT_ISSUE_DATE
		set @DOCUMENT_EXPIRY_DATE = @NORQ_BIOMETRIC_PASSPORT_EXPIRY_DATE
	end
	else if @DOCUMENT_NUMBER=@NORQ_ID_CARD_NUMBER
	begin
		set @DOCUMENT_GIVEN_BY = @NORQ_ID_CARD_ISSUED_BY
		set @DOCUMENT_GIVEN_DATE = @NORQ_ID_CARD_ISSUE_DATE
		set @DOCUMENT_EXPIRY_DATE = @NORQ_ID_CARD_EXPIRY_DATE
	end
	else
	begin
		set @ErrorMessage = N'Սխալ փաստաթղթի տվյալներ'
		RAISERROR (@ErrorMessage, 17, 1)
		RETURN
	end

	BEGIN TRANSACTION

	BEGIN TRY
		insert into Common.APPLICATION
			(CREATION_DATE, ID, SOURCE_ID, LOAN_TYPE_ID, STATUS, INITIAL_AMOUNT, CURRENCY_CODE, CUSTOMER_USER_ID, SHOP_USER_ID, PARTNER_COMPANY_CODE, TO_BE_SYNCHRONIZED
				,FIRST_NAME, LAST_NAME, PATRONYMIC_NAME, BIRTH_DATE, SOCIAL_CARD_NUMBER, ORGANIZATION_ACTIVITY_CODE, MOBILE_PHONE_1
				,DOCUMENT_TYPE_CODE, DOCUMENT_NUMBER,DOCUMENT_GIVEN_BY, DOCUMENT_GIVEN_DATE, DOCUMENT_EXPIRY_DATE, PRODUCT_CATEGORY_CODE, LOAN_TEMPLATE_CODE
				,IS_REFINANCING,COBORROWER_SOCIAL_CARD_NUMBER,COBORROWER_FIRST_NAME,COBORROWER_LAST_NAME,COBORROWER_BIRTH_DATE,COBORROWER_FIRST_NAME_EN,COBORROWER_LAST_NAME_EN
				,COBORROWER_MOBILE_PHONE,COBORROWER_EMAIL,COBORROWER_BIRTH_PLACE_CODE,COBORROWER_CITIZENSHIP_CODE
				,SKIP_OTI,UNIVERSITY_CODE,UNIVERSITY_FACULTY,UNIVERSITY_YEAR,CREATE_CLIENT_ONLY)
		values
			(@CurrentDate, @ID, @SOURCE_ID, @LOAN_TYPE_ID, 3, @INITIAL_AMOUNT, @CURRENCY_CODE, @CUSTOMER_USER_ID, @SHOP_USER_ID, @PARTNER_COMPANY_CODE, 0
				,@FIRST_NAME, @LAST_NAME, @PATRONYMIC_NAME, @BIRTH_DATE, @SOCIAL_CARD_NUMBER, @ORGANIZATION_ACTIVITY_CODE, @MOBILE_PHONE_1
				,@DOCUMENT_TYPE_CODE, @DOCUMENT_NUMBER, @DOCUMENT_GIVEN_BY, @DOCUMENT_GIVEN_DATE, @DOCUMENT_EXPIRY_DATE, @PRODUCT_CATEGORY_CODE, @LOAN_TEMPLATE_CODE
				,@IS_REFINANCING,@COBORROWER_SOCIAL_CARD_NUMBER,@COBORROWER_FIRST_NAME,@COBORROWER_LAST_NAME,@COBORROWER_BIRTH_DATE,@COBORROWER_FIRST_NAME_EN,@COBORROWER_LAST_NAME_EN
				,@COBORROWER_MOBILE_PHONE,@COBORROWER_EMAIL,@COBORROWER_BIRTH_PLACE_CODE,@COBORROWER_CITIZENSHIP_CODE
				,@SKIP_OTI,@UNIVERSITY_CODE,@UNIVERSITY_FACULTY,@UNIVERSITY_YEAR,@CREATE_CLIENT_ONLY)

		insert into Common.NORQ_QUERY_RESULT
			(APPLICATION_ID,FIRST_NAME,LAST_NAME,PATRONYMIC_NAME,BIRTH_DATE,GENDER,
			DISTRICT,COMMUNITY,STREET,BUILDING,APARTMENT,
			PASSPORT_NUMBER,PASSPORT_DATE,PASSPORT_EXPIRY_DATE,PASSPORT_BY,
			BIOMETRIC_PASSPORT_NUMBER,BIOMETRIC_PASSPORT_ISSUE_DATE,BIOMETRIC_PASSPORT_EXPIRY_DATE,BIOMETRIC_PASSPORT_ISSUED_BY,
			ID_CARD_NUMBER,ID_CARD_ISSUE_DATE,ID_CARD_EXPIRY_DATE,ID_CARD_ISSUED_BY,
			SOCIAL_CARD_NUMBER,FEE,SOCIAL_PAYMENT,RESPONSE_XML)
		values
			(@ID,@NORQ_FIRST_NAME,@NORQ_LAST_NAME,@NORQ_PATRONYMIC_NAME,@NORQ_BIRTH_DATE,@NORQ_GENDER,
			@NORQ_DISTRICT,@NORQ_COMMUNITY,@NORQ_STREET,@NORQ_BUILDING,@NORQ_APARTMENT,
			isnull(@NORQ_PASSPORT_NUMBER,''),isnull(@NORQ_PASSPORT_DATE,@CurrentDate),isnull(@NORQ_PASSPORT_EXPIRY_DATE,@CurrentDate),isnull(@NORQ_PASSPORT_BY,''),
			isnull(@NORQ_BIOMETRIC_PASSPORT_NUMBER,''),isnull(@NORQ_BIOMETRIC_PASSPORT_ISSUE_DATE,@CurrentDate),isnull(@NORQ_BIOMETRIC_PASSPORT_EXPIRY_DATE,@CurrentDate),isnull(@NORQ_BIOMETRIC_PASSPORT_ISSUED_BY,''),
			isnull(@NORQ_ID_CARD_NUMBER,''),isnull(@NORQ_ID_CARD_ISSUE_DATE,@CurrentDate),isnull(@NORQ_ID_CARD_EXPIRY_DATE,@CurrentDate),isnull(@NORQ_ID_CARD_ISSUED_BY,''),
			@NORQ_SOCIAL_CARD_NUMBER,@NORQ_FEE,@NORQ_SOCIAL_PAYMENT,@NORQ_RESPONSE_XML)

		insert into Common.NORQ_QUERY_RESULT_WORK
			(APPLICATION_ID,ORGANIZATION_NAME,TAX_ID_NUMBER,ADDRESS,FROM_DATE,TO_DATE,SALARY,SOCIAL_PAYMENT,POSITION)
		select @ID,ORGANIZATION_NAME,TAX_ID_NUMBER,ADDRESS,FROM_DATE,TO_DATE,SALARY,SOCIAL_PAYMENT,POSITION
			from @NORQ_WORK_DATA

		if rtrim(isnull(@COBORROWER_SOCIAL_CARD_NUMBER,''))<>''
		begin
			insert into Common.NORQ_COBORROWER_QUERY_RESULT
				(APPLICATION_ID,FIRST_NAME,LAST_NAME,PATRONYMIC_NAME,BIRTH_DATE,GENDER,
				DISTRICT,COMMUNITY,STREET,BUILDING,APARTMENT,
				PASSPORT_NUMBER,PASSPORT_DATE,PASSPORT_EXPIRY_DATE,PASSPORT_BY,
				BIOMETRIC_PASSPORT_NUMBER,BIOMETRIC_PASSPORT_ISSUE_DATE,BIOMETRIC_PASSPORT_EXPIRY_DATE,BIOMETRIC_PASSPORT_ISSUED_BY,
				ID_CARD_NUMBER,ID_CARD_ISSUE_DATE,ID_CARD_EXPIRY_DATE,ID_CARD_ISSUED_BY,
				SOCIAL_CARD_NUMBER,FEE,SOCIAL_PAYMENT,RESPONSE_XML)
			values
				(@ID,@COBORROWER_FIRST_NAME,@NORQ_COBORROWER_LAST_NAME,@NORQ_COBORROWER_PATRONYMIC_NAME,@NORQ_COBORROWER_BIRTH_DATE,@NORQ_COBORROWER_GENDER,
				@NORQ_COBORROWER_DISTRICT,@NORQ_COBORROWER_COMMUNITY,@NORQ_COBORROWER_STREET,@NORQ_COBORROWER_BUILDING,@NORQ_COBORROWER_APARTMENT,
				isnull(@NORQ_COBORROWER_PASSPORT_NUMBER,''),isnull(@NORQ_COBORROWER_PASSPORT_DATE,@CurrentDate),isnull(@NORQ_COBORROWER_PASSPORT_EXPIRY_DATE,@CurrentDate),isnull(@NORQ_COBORROWER_PASSPORT_BY,''),
				isnull(@NORQ_COBORROWER_BIOMETRIC_PASSPORT_NUMBER,''),isnull(@NORQ_COBORROWER_BIOMETRIC_PASSPORT_ISSUE_DATE,@CurrentDate),isnull(@NORQ_COBORROWER_BIOMETRIC_PASSPORT_EXPIRY_DATE,@CurrentDate),isnull(@NORQ_COBORROWER_BIOMETRIC_PASSPORT_ISSUED_BY,''),
				isnull(@NORQ_COBORROWER_ID_CARD_NUMBER,''),isnull(@NORQ_COBORROWER_ID_CARD_ISSUE_DATE,@CurrentDate),isnull(@NORQ_COBORROWER_ID_CARD_EXPIRY_DATE,@CurrentDate),isnull(@NORQ_COBORROWER_ID_CARD_ISSUED_BY,''),
				@NORQ_COBORROWER_SOCIAL_CARD_NUMBER,@NORQ_COBORROWER_FEE,@NORQ_COBORROWER_SOCIAL_PAYMENT,@NORQ_COBORROWER_RESPONSE_XML)

			insert into Common.NORQ_COBORROWER_QUERY_RESULT_WORK
				(APPLICATION_ID,ORGANIZATION_NAME,TAX_ID_NUMBER,ADDRESS,FROM_DATE,TO_DATE,SALARY,SOCIAL_PAYMENT,POSITION)
			select @ID,ORGANIZATION_NAME,TAX_ID_NUMBER,ADDRESS,FROM_DATE,TO_DATE,SALARY,SOCIAL_PAYMENT,POSITION
				from @NORQ_COBORROWER_WORK_DATA
		end

		insert into Common.ACRA_QUERY_RESULT
			(APPLICATION_ID,FICO_SCORE,RESPONSE_XML)
		values
			(@ID,@ACRA_FICO_SCORE,@ACRA_RESPONSE_XML)

		insert into Common.ACRA_QUERY_RESULT_DETAILS
			(APPLICATION_ID,STATUS,FROM_DATE,TO_DATE,TYPE,CUR,CONTRACT_AMOUNT,DEBT,PAST_DUE_DATE,RISK,CLASSIFICATION_DATE,
				INTEREST_RATE,PLEDGE,PLEDGE_AMOUNT,OUTSTANDING_AMOUNT,OUTSTANDING_PERCENT,BANK_NAME,IS_GUARANTEE,
				DUE_DAYS_1,DUE_DAYS_2,DUE_DAYS_3,DUE_DAYS_4,LOAN_ID)
		select @ID,STATUS,FROM_DATE,TO_DATE,TYPE,CUR,CONTRACT_AMOUNT,DEBT,PAST_DUE_DATE,RISK,CLASSIFICATION_DATE,
				INTEREST_RATE,PLEDGE,PLEDGE_AMOUNT,OUTSTANDING_AMOUNT,OUTSTANDING_PERCENT,BANK_NAME,IS_GUARANTEE,
				DUE_DAYS_1,DUE_DAYS_2,DUE_DAYS_3,DUE_DAYS_4,LOAN_ID
			from @ACRA_DETAILS

		insert into Common.ACRA_QUERY_RESULT_QUERIES
			(APPLICATION_ID,DATE,BANK_NAME)
		select @ID,DATE,BANK_NAME
			from @ACRA_QUERIES

		insert into Common.ACRA_QUERY_RESULT_INTERRELATED
			(APPLICATION_ID,FROM_DATE,TO_DATE,CUR,RISK,CONTRACT_AMOUNT,DUE_AMOUNT,OVERDUE_AMOUNT,OUTSTANDING_PERCENT)
		select @ID,FROM_DATE,TO_DATE,CUR,RISK,CONTRACT_AMOUNT,DUE_AMOUNT,OVERDUE_AMOUNT,OUTSTANDING_PERCENT
			from @ACRA_INTERRELATED

		if rtrim(isnull(@COBORROWER_SOCIAL_CARD_NUMBER,''))<>''
		begin
			insert into Common.ACRA_COBORROWER_QUERY_RESULT
				(APPLICATION_ID,FICO_SCORE,RESPONSE_XML)
			values
				(@ID,@ACRA_COBORROWER_FICO_SCORE,@ACRA_COBORROWER_RESPONSE_XML)

			insert into Common.ACRA_COBORROWER_QUERY_RESULT_DETAILS
				(APPLICATION_ID,STATUS,FROM_DATE,TO_DATE,TYPE,CUR,CONTRACT_AMOUNT,DEBT,PAST_DUE_DATE,RISK,CLASSIFICATION_DATE,
					INTEREST_RATE,PLEDGE,PLEDGE_AMOUNT,OUTSTANDING_AMOUNT,OUTSTANDING_PERCENT,BANK_NAME,IS_GUARANTEE,
					DUE_DAYS_1,DUE_DAYS_2,DUE_DAYS_3,DUE_DAYS_4,LOAN_ID)
			select @ID,STATUS,FROM_DATE,TO_DATE,TYPE,CUR,CONTRACT_AMOUNT,DEBT,PAST_DUE_DATE,RISK,CLASSIFICATION_DATE,
					INTEREST_RATE,PLEDGE,PLEDGE_AMOUNT,OUTSTANDING_AMOUNT,OUTSTANDING_PERCENT,BANK_NAME,IS_GUARANTEE,
					DUE_DAYS_1,DUE_DAYS_2,DUE_DAYS_3,DUE_DAYS_4,LOAN_ID
				from @ACRA_COBORROWER_DETAILS

			insert into Common.ACRA_COBORROWER_QUERY_RESULT_QUERIES
				(APPLICATION_ID,DATE,BANK_NAME)
			select @ID,DATE,BANK_NAME
				from @ACRA_COBORROWER_QUERIES

			insert into Common.ACRA_COBORROWER_QUERY_RESULT_INTERRELATED
				(APPLICATION_ID,FROM_DATE,TO_DATE,CUR,RISK,CONTRACT_AMOUNT,DUE_AMOUNT,OVERDUE_AMOUNT,OUTSTANDING_PERCENT)
			select @ID,FROM_DATE,TO_DATE,CUR,RISK,CONTRACT_AMOUNT,DUE_AMOUNT,OVERDUE_AMOUNT,OUTSTANDING_PERCENT
				from @ACRA_COBORROWER_INTERRELATED
		end

		COMMIT TRANSACTION
	END TRY
	BEGIN CATCH
		ROLLBACK TRANSACTION
		set @ErrorMessage = ERROR_MESSAGE()
		RAISERROR (@ErrorMessage, 17, 1)
		RETURN
	END CATCH
GO



CREATE OR ALTER PROCEDURE Common.sp_GetApplicationForScoring(@ID	uniqueidentifier)
AS
	select isnull(a.ISN,-1) as ISN,a.LOAN_TYPE_ID,a.INITIAL_AMOUNT,a.CURRENCY_CODE
		,Common.ahf_Unicode2ANSI(a.FIRST_NAME) as FIRST_NAME,Common.ahf_Unicode2ANSI(a.LAST_NAME) as LAST_NAME,Common.ahf_Unicode2ANSI(a.PATRONYMIC_NAME) as PATRONYMIC_NAME,a.BIRTH_DATE
		,a.SOCIAL_CARD_NUMBER,a.DOCUMENT_TYPE_CODE,a.DOCUMENT_NUMBER,a.DOCUMENT_GIVEN_BY,a.DOCUMENT_GIVEN_DATE,a.DOCUMENT_EXPIRY_DATE,isnull(a.ORGANIZATION_ACTIVITY_CODE,'') as ORGANIZATION_ACTIVITY_CODE
		,Common.f_GetAge(n.BIRTH_DATE) as AGE,Common.ahf_Unicode2ANSI(n.FIRST_NAME) as NORQ_FIRST_NAME,Common.ahf_Unicode2ANSI(n.LAST_NAME) as NORQ_LAST_NAME,Common.ahf_Unicode2ANSI(n.PATRONYMIC_NAME) as NORQ_PATRONYMIC_NAME,n.BIRTH_DATE as NORQ_BIRTH_DATE,n.GENDER
		,Common.ahf_Unicode2ANSI(n.DISTRICT) as DISTRICT,Common.ahf_Unicode2ANSI(n.COMMUNITY) as COMMUNITY,Common.ahf_Unicode2ANSI(n.STREET) as STREET,Common.ahf_Unicode2ANSI(n.BUILDING) as BUILDING,Common.ahf_Unicode2ANSI(n.APARTMENT) as APARTMENT
		,isnull(n.PASSPORT_NUMBER,'') as PASSPORT_NUMBER,n.PASSPORT_DATE,n.PASSPORT_EXPIRY_DATE,n.PASSPORT_BY
		,isnull(n.BIOMETRIC_PASSPORT_NUMBER,'') as BIOMETRIC_PASSPORT_NUMBER,n.BIOMETRIC_PASSPORT_ISSUE_DATE,n.BIOMETRIC_PASSPORT_EXPIRY_DATE,n.BIOMETRIC_PASSPORT_ISSUED_BY
		,isnull(n.ID_CARD_NUMBER,'') as ID_CARD_NUMBER,n.ID_CARD_ISSUE_DATE,n.ID_CARD_EXPIRY_DATE,n.ID_CARD_ISSUED_BY
		,n.SOCIAL_CARD_NUMBER as NORQ_SOCIAL_CARD_NUMBER,n.FEE,n.SOCIAL_PAYMENT
		,c.FICO_SCORE
		,coalesce(a.MOBILE_PHONE_1,au.LOGIN,'') as MOBILE_PHONE_1,isnull(au.EMAIL,'') as EMAIL,isnull(cu.FIRST_NAME_EN,'') as FIRST_NAME_EN,isnull(cu.LAST_NAME_EN,'') as LAST_NAME_EN
		,isnull(a.PRODUCT_CATEGORY_CODE,'') as PRODUCT_CATEGORY_CODE,isnull(a.LOAN_TEMPLATE_CODE,'') as LOAN_TEMPLATE_CODE
		,a.NORQ_TRY_COUNT,a.ACRA_TRY_COUNT,a.IS_REFINANCING
		,a.COBORROWER_SOCIAL_CARD_NUMBER,Common.ahf_Unicode2ANSI(isnull(a.COBORROWER_FIRST_NAME,'')) as COBORROWER_FIRST_NAME,Common.ahf_Unicode2ANSI(isnull(a.COBORROWER_LAST_NAME,'')) as COBORROWER_LAST_NAME
		,a.COBORROWER_BIRTH_DATE,a.COBORROWER_FIRST_NAME_EN,a.COBORROWER_LAST_NAME_EN,a.COBORROWER_MOBILE_PHONE,a.COBORROWER_EMAIL,a.COBORROWER_BIRTH_PLACE_CODE,a.COBORROWER_CITIZENSHIP_CODE
		,isnull(a.SKIP_OTI,0) as SKIP_OTI
		,a.UNIVERSITY_CODE,a.UNIVERSITY_FACULTY,a.UNIVERSITY_YEAR
		,Common.f_GetAge(nc.BIRTH_DATE) as COBORROWER_AGE,Common.ahf_Unicode2ANSI(nc.FIRST_NAME) as NORQ_COBORROWER_FIRST_NAME,Common.ahf_Unicode2ANSI(nc.LAST_NAME) as NORQ_COBORROWER_LAST_NAME,Common.ahf_Unicode2ANSI(nc.PATRONYMIC_NAME) as NORQ_COBORROWER_PATRONYMIC_NAME,nc.BIRTH_DATE as NORQ_COBORROWER_BIRTH_DATE,nc.GENDER as COBORROWER_GENDER
		,Common.ahf_Unicode2ANSI(nc.DISTRICT) as COBORROWER_DISTRICT,Common.ahf_Unicode2ANSI(nc.COMMUNITY) as COBORROWER_COMMUNITY,Common.ahf_Unicode2ANSI(nc.STREET) as COBORROWER_STREET,Common.ahf_Unicode2ANSI(nc.BUILDING) as COBORROWER_BUILDING,Common.ahf_Unicode2ANSI(nc.APARTMENT) as COBORROWER_APARTMENT
		,isnull(nc.PASSPORT_NUMBER,'') as COBORROWER_PASSPORT_NUMBER,nc.PASSPORT_DATE as COBORROWER_PASSPORT_DATE,nc.PASSPORT_EXPIRY_DATE as COBORROWER_PASSPORT_EXPIRY_DATE,nc.PASSPORT_BY as COBORROWER_PASSPORT_BY
		,isnull(nc.BIOMETRIC_PASSPORT_NUMBER,'') as COBORROWER_BIOMETRIC_PASSPORT_NUMBER,nc.BIOMETRIC_PASSPORT_ISSUE_DATE as COBORROWER_BIOMETRIC_PASSPORT_ISSUE_DATE,nc.BIOMETRIC_PASSPORT_EXPIRY_DATE as COBORROWER_BIOMETRIC_PASSPORT_EXPIRY_DATE,nc.BIOMETRIC_PASSPORT_ISSUED_BY as COBORROWER_BIOMETRIC_PASSPORT_ISSUED_BY
		,isnull(nc.ID_CARD_NUMBER,'') as COBORROWER_ID_CARD_NUMBER,nc.ID_CARD_ISSUE_DATE as COBORROWER_ID_CARD_ISSUE_DATE,nc.ID_CARD_EXPIRY_DATE as COBORROWER_ID_CARD_EXPIRY_DATE,nc.ID_CARD_ISSUED_BY as COBORROWER_ID_CARD_ISSUED_BY
		,nc.SOCIAL_CARD_NUMBER as NORQ_COBORROWER_SOCIAL_CARD_NUMBER,nc.FEE as COBORROWER_FEE,nc.SOCIAL_PAYMENT as COBORROWER_SOCIAL_PAYMENT
		,cc.FICO_SCORE as COBORROWER_FICO_SCORE
		,isnull(a.CREATE_CLIENT_ONLY,0) as CREATE_CLIENT_ONLY
	from Common.APPLICATION a with (NOLOCK)
	join Common.NORQ_QUERY_RESULT n with (NOLOCK)
		on n.APPLICATION_ID=a.ID
	join Common.ACRA_QUERY_RESULT c with (NOLOCK)
		on c.APPLICATION_ID=a.ID
	left join Common.NORQ_COBORROWER_QUERY_RESULT nc with (NOLOCK)
		on nc.APPLICATION_ID=a.ID
	left join Common.ACRA_COBORROWER_QUERY_RESULT cc with (NOLOCK)
		on cc.APPLICATION_ID=a.ID
	left join Common.CUSTOMER_USER cu with (NOLOCK)
		on cu.APPLICATION_USER_ID=a.CUSTOMER_USER_ID
	left join Common.APPLICATION_USER au with (NOLOCK)
		on au.ID=cu.APPLICATION_USER_ID
	where a.ID=@ID
GO
